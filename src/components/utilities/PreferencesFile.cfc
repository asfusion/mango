<!--- 
Implementation of the Java Preferences API. 
Although it doesn't implement the same functions as the java.util.prefs.Preferences, it is able to parse
and modify files generated by the Java API.

It can be used to store any type of configuration settings or user
preferences whenever programatic access and manipulation is desired.

Sample usage:
<cfset preferencesFile = GetDirectoryFromPath(GetCurrentTemplatePath()) &  "myPreferences.cfm" />
<cfset preferences = createObject("component","components.utilities.Preferences").init(preferencesFile) />
Path: /userSettings/tom 
Key: name
Value: Tom
<cfset preferences.put("/userSettings/tom","name", "Tom") />

Path: /userSettings/tom 
Key: themeColor
Value: red
<cfset preferences.put("/userSettings/tom","themeColor", "red") />

--------------------------------------------------
Copyright 2007 Laura Arguello

Licensed under the Apache License, Version 2.0 (the "License"); you may not use the files in this package except in compliance with the License. You may obtain a copy of the License at 

http://www.apache.org/licenses/LICENSE-2.0 

Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, 
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
See the License for the specific language governing permissions and limitations under the License.
--->

<cfcomponent extends="Preferences">

	<cfset variables.fileStorage = "" />
	
	<!--- ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::: --->	
	<cffunction name="init" access="public" output="false" hint="Contructor">
		<cfargument name="fileStorage" type="String" required="true" hint="File where to store the preferences" />
		<cfargument name="node" type="String" required="false" default="/preferences/root" hint="Starting node. Not required" />
			
			<cfset var data = "" />
			
			<cfset variables.fileStorage = arguments.fileStorage />
			
			<cfif fileexists(arguments.fileStorage)>
				<!--- read the file and import it --->
				<cffile action="read" file="#variables.fileStorage#" variable="data">
				<!--- remove cfsilent just in case --->
				<cfset data = replacenocase(replacenocase(data,"<cfsilent>",""),"</cfsilent>","") />
				<cfset super.init(data,arguments.node) />
			<cfelse>
				<cfset super.init("",arguments.node) />
			</cfif>
			
		<cfreturn this />
	</cffunction>

	
<!--- ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::: --->	
	<cffunction name="flush" access="public" output="false" returntype="void" hint="Saves any changes in the contents of the preferences to the persistent store">
	
		<cffile action="write" file="#variables.fileStorage#" output="<cfsilent>#trim(toString(variables.xmlData))#</cfsilent>">
			
	</cffunction>		
	
</cfcomponent>